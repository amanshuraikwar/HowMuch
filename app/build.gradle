apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: "$project.rootDir/script-git-version.gradle"

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "io.github.amanshuraikwar.howmuch"
        minSdkVersion 21
        targetSdkVersion 28
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    ext{
        APP_NAME_DEV = "@string/app_name_dev"
        APP_NAME_PROD = "@string/app_name_prod"
        APP_NAME_USABLE = "@string/app_name_usable"
    }

    buildTypes {

        debug {
            applicationIdSuffix ".debug"
        }

        release {
            applicationIdSuffix ".release"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }

    // automatic version code and version name from git tag
    flavorDimensions "version"

    productFlavors {

        dev {
            applicationIdSuffix ".dev"
            versionCode gitVersionCodeTime
            versionName gitVersionName + "-DEV"
            manifestPlaceholders = [ applicationLabel : APP_NAME_DEV]
        }

        usable {
            applicationIdSuffix ".usable"
            versionCode gitVersionCodeTime
            versionName gitVersionName + "-USABLE"
            manifestPlaceholders = [ applicationLabel : APP_NAME_USABLE]
        }

        prod {
            applicationIdSuffix ".prod"
            versionCode gitVersionCode
            versionName gitVersionName
            manifestPlaceholders = [ applicationLabel : APP_NAME_PROD]
        }
    }

    // apk file name
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = new File("HowMuch-" + variant.versionName + "-(" + variant.versionCode + ")-" + variant.buildType.name + ".apk")
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}

dependencies {

    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"

    implementation "com.android.support:preference-v7:28.0.0"

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test:runner:$testRunnerVersion"
    androidTestImplementation("androidx.test.espresso:espresso-core:$espressoVersion") {
        exclude group: 'com.google.code.findbugs'
    }
    androidTestImplementation "androidx.annotation:annotation:$annotationVersion"

    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.cardview:cardview:$cardviewVersion"

    // Dagger
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerAndroidVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerAndroidVersion"

    // Rx
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"

    // room
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    kapt "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    // retrofit - api calls
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    implementation "com.google.code.gson:gson:$gsonVersion"

    implementation "com.google.android.gms:play-services-auth:$playServicesAuthVersion"

    implementation("com.google.api-client:google-api-client-android:$googleApiClientVersion") {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation("com.google.apis:google-api-services-sheets:$googleSheetsApiVersion") {
        exclude group: 'org.apache.httpcomponents'
    }

    implementation "me.zhanghai.android.materialprogressbar:library:$materialProgressBarVersion"

    // glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    implementation project(':protocol')
    implementation project(':base')
    implementation project(':googlesheetsprotocol')
    implementation project(':multiitemlistadapter')

    // https://stackoverflow.com/questions/52521302/how-to-solve-program-type-already-present-com-google-common-util-concurrent-lis
    configurations {
        all*.exclude group: 'com.google.guava', module: 'listenablefuture'
    }
}
